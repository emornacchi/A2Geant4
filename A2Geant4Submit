#!/usr/bin/perl
use strict;
use warnings;
use Getopt::Long;

use Cwd qw(abs_path getcwd);
use File::Path qw(rmtree mkpath);
use File::Basename qw(basename);

my $filelist = '-';             # default STDIN
my $macro;                      # needs to be provided on cmd line
my $outputdir;                  # needs to be provided on cmd line
my $clean = 0;
my $help = 0;
my $cwd = getcwd;
my $QSUB_BIN = 'qsub';
my $A2GEANT4_BIN = which('A2Geant4');
my @goodnodes=("blade-01.farm.kph", "blade-02.farm.kph", "blade-21.farm.kph", "blade-22.farm.kph", "blade-01.farm.kph", "blade-02.farm.kph", "blade-21.farm.kph", "blade-22.farm.kph", "blade-20.farm.kph");
# parse options
Getopt::Long::Configure(qw(gnu_getopt));
GetOptions(
           'help|h' => \$help,
           'clean|c' => \$clean
          ) or print_help();


# parse arguments
if (@ARGV==2 || @ARGV==3) {
  $macro = $ARGV[0];
  $outputdir = $ARGV[1];
  if (@ARGV==3) {
    $filelist = $ARGV[2];
  }
} else {
  print_help();
}


&main;

sub main {
  # check some things before submission
  die "A2Geant4 not found in PATH=$ENV{PATH}" unless defined $A2GEANT4_BIN;
  warn "Warning: Current working directory '$cwd' does not end with 'A2Geant4'" unless $cwd =~ /A2Geant4$/;

  # check for top-level config files
  my $haveMacro = -f $macro ? 1 : 0;

  die "Error: $macro macro not found. Are you in the wrong working directory?"
    unless $haveMacro;


  # read in the filelist, check for more errors
  open(my $inputfiles_fh, "<$filelist") or die "Can't open filelist $filelist: $!";
  my $n = 0;
  my @files;
  while (my $inputfile = <$inputfiles_fh>) {
    $n++;
    # remove leading and trailing whitespace
    $inputfile =~ s/^\s+//;
    $inputfile =~ s/\s+$//;
    unless(-f $inputfile) {
      warn "Warning: Inputfile $inputfile does not exist, skipping.";
      next;
    }
    unless($inputfile =~ /\.root$/) {
      warn "Warning: Inputfile $inputfile does not end with .root, skipping.";
      next;
    }

    my $runnumber = $n;
    $inputfile = abs_path($inputfile);
    push(@files, [$inputfile, $runnumber]);
  }
  close $inputfiles_fh;

  my $total = scalar @files;
  die "No files to be submitted, abort." if $total==0;

  # clean/create outputdir
  if ($clean) {
    print "Deleting outputdir $outputdir\n";
    rmtree([$outputdir]) or die "Error: Outputdir $outputdir was not deleted.";
  }
  die "Error: Output dir '$outputdir' already or still exists. Maybe use --clean?" if -e $outputdir;
  mkpath(["$outputdir/log", "$outputdir/root"]) == 3 # three folders should be created
    or die "Error: Cannot create log and root folders below outputdir $outputdir";


  # for jobs, make it absolute
  $outputdir = abs_path($outputdir);
  $A2GEANT4_BIN = abs_path($A2GEANT4_BIN);

  # finally submit the jobs
  my $submitted = 0;
  for my $f (@files) {
    submit_file($f->[0], $f->[1]);
    $submitted++;
    printf("%-40s\r",sprintf("%04d/%04d = %03.0f %% submitted",$submitted,$total,100*$submitted/$total));
  }
  printf("%-40s\n", "Submitted $submitted jobs.");
}

sub which {
  my $cmd = shift;
  open(my $p, "which $cmd |") or die "Can't open which: $!";
  my @lines = <$p>;
  close $p;
  my $exit_value = $? >> 8;
  if ($exit_value != 0 || @lines != 1) {
    return undef;
  }
  chomp $lines[0];
  return $lines[0];
}

sub submit_file {
  my $inputfile = shift;
  my $runnumber = shift;

  my $ncpus = 1;

  my $basename = basename($inputfile);

  my $outputfile = $basename;
  $outputfile =~ s/AcquMC/A2G4/;
  #$outputfile =~ s/EventGen/A2G4/;
  #$outputfile =~ s/Pluto/A2G4/;
  #$outputfile =~ s/GiBUU/A2G4/;
  my $arguments = "--mac=$macro --if=$inputfile --of=$outputdir/root/$outputfile";
  my $user = $ENV{USER};
  my $NODES = $goodnodes[ rand @goodnodes ];
  # open pipe to qsub
  open(my $qsub, "| $QSUB_BIN >/dev/null")
    or die "Can't open pipe to $QSUB_BIN: $!";
  print $qsub <<__BATCHSCRIPT;
#!/bin/bash
#PBS -N A2Geant4_$runnumber
## Send eMail if job aborts
#PBS -m a
## Send eMail when job begins
##PBS -m b
## Send eMail if job ends
##PBS -m e
#PBS -M $user\@kph.uni-mainz.de
#PBS -V
#PBS -l walltime=12:00:00
#PBS -j oe
#PBS -o $outputdir/log/$basename.log
#PBS -q batch
#PBS -l ncpus=$ncpus

echo INPUTFILE: $inputfile
cd $cwd
$A2GEANT4_BIN $arguments
__BATCHSCRIPT
  close $qsub or die "Cannot close pipe to qsub";
}

sub print_help {
  print <<__EOF;
Usage: A2Geant4Submit [--clean] <macro> <outputdir> [<filelist>|-]

Submit jobs with qsub running A2Geant4 on files in provided <filelist>
(or read it from STDIN), output goes to <outputdir> (will be created
and must not exist already). Needs to be run from "A2Geant4"
directory with proper <macro> existing.

Options:

  --clean   Recursively delete <outputdir> before submission.
__EOF
  exit 255;
}
